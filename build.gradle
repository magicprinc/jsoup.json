plugins {
  id 'java-library'
  id 'idea'
  id 'maven-publish'
  id "net.ltgt.errorprone" version "latest.release"
}
repositories {
	mavenLocal()
  mavenCentral()
  maven { url = "https://jitpack.io" }
}
println "[INFO] ${project.group}:${project.name}:$version  ⇒  ${tasks.jar.archiveFileName.get()} # JVM: ${System.getProperty("java.version")}  Gradle: ${gradle.gradleVersion}"

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8" // = compileJava.options.encoding = "UTF-8"
	options.compilerArgs.addAll(["-Xlint:all,-serial", "-parameters"])
	options.release.set(17)// javac --release 7→8..23+
	options.deprecation = true
	options.debug = true

	options.errorprone {
		enabled = true
		disableWarningsInGeneratedCode = true
		excludedPaths = ".*/(generated|test).*/.*"
		disable("UnusedVariable")
		disable("MissingSummary")
		disable("StringConcatToTextBlock")
		errorproneArgs = ["-XepExcludedPaths:.*/test/.*"]
	}
}


dependencies {
  errorprone "com.google.errorprone:error_prone_core:latest.release"

  //works since 1.12, but XML escaping is better and XML is more compact since 1.14.2
  //api vs implementation: jsoup is 100% already declared, if we are used
  implementation('org.jsoup:jsoup:1.18.1')
	implementation("org.jspecify:jspecify:1.+")

	// TEST
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.+'// API for writing tests
	testImplementation("org.junit.jupiter:junit-jupiter:5.13.+")
	testImplementation("org.junit.vintage:junit-vintage-engine:5.13.+")
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.13.+'// Platform launcher

  testImplementation 'com.google.code.gson:gson:2.+'
}


test {
  useJUnitPlatform()
  maxHeapSize = '256M'
  enableAssertions = true

  testLogging.showStandardStreams = true // show standard out & err of the test JVM on the console

  System.setProperty("file.encoding", "UTF-8")
}


java { // https://docs.gradle.org/current/userguide/publishing_maven.html
  withSourcesJar()
  withJavadocJar()
}
// https://docs.gradle.org/current/userguide/publishing_maven.html
publishing {
  publications {
		release(MavenPublication) { // artifactId = folder name = 'json4jsoup'
      from components.java
			suppressPomMetadataWarningsFor('runtimeElements')
    }
  }
}


idea { module { downloadJavadoc = true;	downloadSources = true } }

jar {
	from generatePomFileForReleasePublication {
		rename('pom-default.xml', "META-INF/maven/${project.group}/${project.name}/pom.xml")
	}
}

tasks.withType(AbstractArchiveTask).configureEach {
	preserveFileTimestamps = false
	reproducibleFileOrder = true
}